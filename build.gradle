plugins {
    id 'java'
    id 'de.undercouch.download' version '3.2.0'
}

group = 'automation'
version = '1.0-SNAPSHOT'

description = "Automated Selenium tests for e-shop"

repositories {
    jcenter()
}

dependencies {
    testCompile 'org.seleniumhq.selenium:selenium-java:3.4.0'
    testCompile 'info.cukes:cucumber-java:1.2.5', 'info.cukes:cucumber-junit:1.2.5'
}

apply from: 'driver.gradle'
defaultTasks 'downloadDrivers'

task executeCucumberFeatures(type: Test, dependsOn: downloadDrivers) {
    def htmlReportPath = 'build/cucumber/report'
    jvmArgs '-Dcucumber.options= ' +
            '--plugin pretty ' +
            '--plugin json:build/cucumber/cucumber.json ' +
            "--plugin html:$htmlReportPath"

    testLogging {
        events "skipped", "failed", "passed", "standard_out"
    }

    doLast {
        logger.lifecycle "See Cucumber report at $projectDir/$htmlReportPath/index.html"
        logger.lifecycle "See also standard gradle report at ${reports.html.entryPoint.toString()}"
    }
}